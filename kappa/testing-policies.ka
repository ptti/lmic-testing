////
//// Constants of the simulation
////
//// These parameters are held fixed across all scenarios, with the
//// exception of N which is typically overridden for larger 
//// simulations
////
//%var: beta         0.034    // probability of infection from contact
//%var: c            6.5      // contact rate
%var: k_pro        1.0/4    // progression from exposed to prodromic
%var: k_inf        1.0      // progression from prodromic to full disease
%var: p_asym       0.5      // probability of being asymptomatic
%var: p_sym        1.0-p_asym  // probability of being symptomatic
%var: k_rem        1.0/7    // removal by recovery or death
%var: k_iso        12       // rate of isolation -- within 2 hours
%var: k_exit       1.0/14   // rate of leaving isolation
%var: k_retest     1.0/14   // rate of allowing retesting
%var: k_flu        1.0/365  // rate of getting the flu, once per year
%var: k_death      1.0/14   // rate of dying from critical illness
%var: d_flu        7        // duration of flu
%var: p_flu        d_flu*k_flu // prevalence of flu
%var: theta        thetaS + thetaI - thetaS*thetaI
%var: m            0.1*N    // rate of manufacturing tests
%var: s	           1.0      // specificity - true negatives per negative
%var: r            0.98	    // recall - true positives per positives for high viral load
%var: ifr          0.005    // infection fatality rate

////
//// These parameters are set in specific scenario files
////
// %var: p_iso     1.0      // fraction of population prone to self-isolating
// %var: k_result  1.0      // time to get test result
// %var: thetaS    0.0      // rate of surveillance testing
// %var: thetaI    1.0      // rate of testing of isolated population
// %var: eta       0.45     // trace 90% of contacts in two days
// %var: p_low     0.0      // chance of low viral load people developing symptoms
// %var: p_high    1.0      // chance of high viral load people developing symptoms

////
//// Agents, or participants in the simulation
////

// individuals have several internal states:
//   - x corresponds to the standard SEIR compartments
//   - c covid symptomaticity: prodromic, asymptomatic, symptomatic
//   - v covid viral load: low or high
//   - s symptomaticity from covid, cold, flu, etc
//   - t indicates whether testing should give positive or negative
//   - r test result, x for untested
//   - q isolation status
//   - f infected with the flu, or not
//   - i prone to self-isolation or not
//   - o outcome - recovered or dying
%agent: P(x{s e i r} c{p a s} s{y n} t{y n} r{x y n} q{y n} f{y n} i{y n} o{r c d}, v{=0 / += 11})
// tests are discrete entities and may be scarce, when being used,
// they are bound to an individual, and are used or not
%agent: T(u{y n})
// traces follow the same disease progression as individuals, and flu
%agent: C(x{s e i r f})
// record of individuals traced
%agent: Tr()

////
//// I. Rules relating to infectious disease infection and progression
////

'tick_up_0'      P(x{i}, c{p}, v{=0 / += 1}) @ 2
'tick_up_1'      P(x{i}, c{p}, v{=1 / += 1}) @ 2
'tick_up_2'      P(x{i}, c{p}, v{=2 / += 1}) @ 2
'tick_up_3'      P(x{i}, c{p}, v{=3 / += 1}) @ 2
'tick_up_4'      P(x{i}, c{p}, v{=4 / += 1}) @ 2
'tick_up_5'      P(x{i}, c{p}, v{=5 / += 1}) @ 2
'tick_up_6'      P(x{i}, c{p}, v{=6 / += 1}) @ 2
'tick_up_7'      P(x{i}, c{p}, v{=7 / += 1}) @ 2
'tick_up_8'      P(x{i}, c{p}, v{=8 / += 1}) @ 2
'tick_up_9'      P(x{i}, c{p}, v{=9 / += 1}) @ 2
'tick_down_sym'  P(x{i}, c{s}, v{>=1 / -= 1}) @ 0.5
'tick_down_asym' P(x{i}, c{a}, v{>=1 / -= 1}) @ 0.5

// 1 - contact rules for unconfined individuals with the covid
'infection'   P(x{s}, q{n}), P(x{i}, q{n}, v{=vl}), . ->
              P(x{e}, q{n}), P(x{i}, q{n}), C(x{e})   @ 0.1*vl*beta*c/N

'lucky'       P(x{s}, q{n}), P(x{i}, q{n}, v{=vl}), . ->
              P(x{s}, q{n}), P(x{i}, q{n}), C(x{s})   @ (1-0.1*vl*beta)*c/N

'exposed'     P(x{e}, q{n}), P(x{i}, q{n}), . ->
              P(x{e}, q{n}), P(x{i}, q{n}), C(x{e})   @ c/N

'infected'    P(x{i}, q{n}), P(x{i}, q{n}), . ->
              P(x{i}, q{n}), P(x{i}, q{n}), C(x{i})   @ c/N

'immune'      P(x{r}, q{n}), P(x{i}, q{n}), . ->
              P(x{r}, q{n}), P(x{i}, q{n}), C(x{r})   @ c/N

'flu'         P(q{n}), P(f{y}, q{n}), . ->
              P(q{n}), P(f{y}, q{n}), C(x{f})         @ c/N

// 2 - progression of individuals to infectiousness. exposed individuals
//     become infectious, at first prodromic, and should test positively
'prog_pro'   P(x{e/i}, c{#/p}, t{n/y})  @ k_pro

// 3 - progression to asymptomatic for a fraction of prodromic individuals
'prog_asym'  P(x{i}, c{p/a}, s{#/n}) @ k_inf*p_asym

// 4 - progression to symptomatic for the remainder
'prog_sym'   P(x{i}, c{p/s}, s{#/y}) @ k_inf*(1-p_asym)

// 5 - removal of asymptomatic individuals, no longer test positively
'rem_asym'   P(x{i/r}, c{a}, t{y/n}, o{#/r}) @ k_rem
// 6 - removal of symptomatic individuals, no longer test positively, and recover
'rem_sym_r'  P(x{i/r}, c{s}, t{y/n}, s{y/n}, o{#/r}) @ (1-ifr/p_sym) * k_rem
// 7 - removal of symptomatic individuals who will not recover and remain critically ill
'rem_sym_c'  P(x{i/r}, c{s}, s{y}, o{#/c}) @ ifr/p_sym*k_rem

// 7 - development and clearing of endemic flu symptoms
'flu_dev'    P(f{n}, s{n}) <-> P(f{y}, s{y}) @ k_flu, 1/d_flu

// 8 - progression to death for those that do not recover
'death'      P(o{c/d}) @ k_death

////
//// II. Rules relating to testing
////

// manufacturing and disposal of tests
'manufacturing' . -> T(u[.]{n}) @ m

// 1 - test randomly selected individuals
'surveillance'  P(r{x}, t[.]), T(u[.]{n}) -> P(r{x}, t[1]), T(u[1]{y}) @ thetaS

// 2 - test those who have been isolated and are untested
'isolated'    P(q{y}, r{x}, t[.]), T(u[.]{n}) -> P(q{y}, r{x}, t[1]), T(u[1]{y}) @ thetaI

// 3 - test results, true negatives and positives
'test_tp'     P(t[_]{y}, r{x/y}) @ r*k_result
'test_fn'     P(t[_]{y}, r{x/n}) @ (1-r)*k_result
'test_tn'     P(t[_]{n}, r{x/n}) @ s*k_result
'test_fp'     P(t[_]{n}, r{x/y}) @ (1-s)*k_result

// 4 - allow for retesting after a time, release the test
'retest'      P(t[1/.], r{#/x}), T(u[1/.]) @ k_retest

////
//// III. Rules relating to isulation
////

// 1 - self-isolate because of symptoms
'iso_sym'   P(s{y}, i{y}, q{n/y}) @ k_iso

// 2 - isolate because of test
'iso_test'  P(r{y}, q{n/y}) @ k_iso

// 3 - rapidly exit from isolation on a negative test
'exit_test' P(r{n}, q{y/n}) @ k_iso

// 4 - exit from isolation if no symptoms
'exit_s'    P(x{s}, q{y/n})             @ k_exit
'exit_e'    P(x{e}, q{y/n})             @ k_exit
'exit_ip'   P(x{i}, c{p}, r{n}, q{y/n}) @ k_exit // prodromic
'exit_ia'   P(x{i}, c{a}, r{n}, q{y/n}) @ k_exit // asymptomatic
'exit_ipx'  P(x{i}, c{p}, r{x}, q{y/n}) @ k_exit // prodromic
'exit_iax'  P(x{i}, c{a}, r{x}, q{y/n}) @ k_exit // asymptomatic
'exit_r'    P(x{r}, q{y/n})             @ k_exit

////
//// IV. Rules relating to tracing
////

// 1. progression of traces parallel to individuals wthout
//    distinguishing severity
'c_progression' C(x{e/i}) @ k_pro
'c_removal'     C(x{i/r}) @ 1.0/(1.0/k_inf + 1.0/k_rem)
'c_degradation' C() -> .  @ k_rem

// 2. trace those who have had contact with positive
//    tested individuals
'trace_s'     P(x{s}, q{#}), P(r{y}), C(x{s}), . ->
              P(x{s}, q{y}), P(r{y}), ., Tr()         @ eta*theta/N
'trace_e'     P(x{e}, q{#}), P(r{y}), C(x{e}), . ->
              P(x{e}, q{y}), P(r{y}), ., Tr()         @ eta*theta/N
'trace_i'     P(x{i}, q{#}), P(r{y}), C(x{i}), . ->
              P(x{i}, q{y}), P(r{y}), ., Tr()         @ eta*theta/N
'trace_r'     P(x{r}, q{#}), P(r{y}), C(x{r}), . ->
              P(x{r}, q{y}), P(r{y}), ., Tr()         @ eta*theta/N

// 3. trace those who have had contact with flu case
'trace_f'     P(q{#}), P(r{y}), C(x{f}), . ->
              P(q{y}), P(r{y}), ., Tr()               @ eta*theta/N

////
//// Observables, or things that we want to know from the simulation
////
%obs: Sn    |P(x{s}, q{n})|
%obs: En    |P(x{e}, q{n})|
%obs: In    |P(x{i}, q{n})|
%obs: Rn    |P(x{r}, q{n})|
%obs: Sy    |P(x{s}, q{y})|
%obs: Ey    |P(x{e}, q{y})|
%obs: Iy    |P(x{i}, q{y})|
%obs: Ry    |P(x{r}, q{y})|
%obs: H     |P(o{c})|*5
%obs: M     |P(o{d})|
%obs: F     |P(f{y})|
%obs: Cs    |C(x{s})|
%obs: Ce    |C(x{e})|
%obs: Ci    |C(x{i})|
%obs: Cr    |C(x{r})|
%obs: Cf    |C(x{f})|
%obs: Tr    |Tr()|
%obs: T     |T()|
%obs: Tbusy |T(u[_])|
%obs: Tused |T(u{y})|
%obs: Tpos  |P(r{y})|
%obs: Tneg  |P(r{n})|
%obs: contact c
%obs: v0    |P(v{=0})|
%obs: v1    |P(v{=1})|
%obs: v2    |P(v{=2})|
%obs: v3    |P(v{=3})|
%obs: v4    |P(v{=4})|
%obs: v5    |P(v{=5})|
%obs: v6    |P(v{=6})|
%obs: v7    |P(v{=7})|
%obs: v8    |P(v{=8})|
%obs: v9    |P(v{=9})|
%obs: v10   |P(v{=10})|
%obs: v11   |P(v{=11})|
%obs: v12   |P(v{>=12})|

// initially infectious
// initially susceptible
%var: INIT_S N - INIT_I

%init: INIT_I*p_iso     P(x{i}, c{p}, s{n}, t{n}, r{x}, q{n}, f{n}, i{y}, o{r}, v{=0})
%init: INIT_I*(1-p_iso) P(x{i}, c{p}, s{n}, t{n}, r{x}, q{n}, f{n}, i{n}, o{r}, v{=0})
%init: INIT_S*p_iso     P(x{s}, c{p}, s{n}, t{n}, r{x}, q{n}, f{n}, i{y}, o{r}, v{=0})
%init: INIT_S*(1-p_iso) P(x{s}, c{p}, s{n}, t{n}, r{x}, q{n}, f{n}, i{n}, o{r}, v{=0})

